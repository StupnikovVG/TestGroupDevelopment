Процедура ОбработкаПроведения(Отказ, РежимПроведения)         
	//  1. Получение запросом данных документа     
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДокументНоменклатура.Номенклатура КАК Номенклатура,
	|	СУММА(ДокументНоменклатура.Количество) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	Документ.Продажа.Номенклатура КАК ДокументНоменклатура
	|ГДЕ
	|	ДокументНоменклатура.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументНоменклатура.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Количество КАК Количество
	|ИЗ
	|	Товары КАК Товары";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	//  2. Формирование движений-расход регистра     
	Движения.ОстаткиТоваров.Очистить();
	ВыборкаТовары = РезультатЗапроса.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл         
		Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаТовары.Номенклатура;
		Движение.Количество = ВыборкаТовары.Количество;
	КонецЦикла;
	//  3. Запись движений в БД     
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();
	
	//  4. Запрос, получающий отрицательные остатки из регистра     
	Запрос.Текст = "ВЫБРАТЬ
	|	Остатки.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Остатки.Номенклатура) КАК НоменклатураПредставление,
	|	-Остатки.КоличествоОстаток КАК Дефецит
	|ИЗ
	|	РегистрНакопления.ОстаткиТоваров.Остатки(
	|			&МоментВремени,
	|			Номенклатура В
	|				(ВЫБРАТЬ
	|					Товары.Номенклатура КАК Номенклатура
	|				ИЗ
	|					Товары КАК Товары)) КАК Остатки
	|ГДЕ
	|	Остатки.КоличествоОстаток < 0";
	ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
	РезультатЗапроса = Запрос.Выполнить();
	//  5. Вывод сообщений о недостатке товаров     
	Если Не РезультатЗапроса.Пустой() Тогда         
		Отказ = Истина;
		ВыборкаОшибки = РезультатЗапроса.Выбрать();
		Пока ВыборкаОшибки.Следующий() Цикл             
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Товара "+ВыборкаОшибки.НоменклатураПредставление+" недостаточно в количестве "+ВыборкаОшибки.Дефецит+" шт.";
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
	
		Возврат;
	
	КонецЕсли; 
	
	//todo списание партий
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Документ успешно проведен.";
	Сообщение.Сообщить();
	
КонецПроцедуры   